plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'com.google.android.libraries.mapsplatform.secrets-gradle-plugin'
    id 'com.google.android.gms.oss-licenses-plugin'
    id 'androidx.navigation.safeargs.kotlin'
    id 'org.jlleitschuh.gradle.ktlint' version "10.1.0"
}

android {
    compileSdkVersion 30

    defaultConfig {
        applicationId "com.sharkaboi.mediahub"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 1
        versionName "1.2"

        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro'
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding true
    }

    secrets {
        propertiesFileName 'secrets.properties'
        defaultPropertiesFileName = 'secrets.defaults.properties'
    }
}

dependencies {
    //core
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.annotation:annotation:1.2.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation "androidx.fragment:fragment-ktx:1.3.5"
    //hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"
    //networking
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation 'com.squareup.okhttp3:logging-interceptor:5.0.0-alpha.2'
    implementation "com.squareup.retrofit2:converter-moshi:2.9.0"
    implementation "com.github.haroldadmin:NetworkResponseAdapter:4.1.0"
    //moshi
    implementation "com.squareup.moshi:moshi-kotlin:1.12.0"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:1.12.0"
    //navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"
    //datastore
    implementation "androidx.datastore:datastore-preferences:1.0.0-rc01"
    //coil
    implementation 'io.coil-kt:coil:1.2.2'
    //paging
    implementation "androidx.paging:paging-runtime-ktx:$paging_version"
    //design
    implementation 'com.github.razir.progressbutton:progressbutton:2.1.0'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'
    implementation "com.airbnb.android:lottie:3.7.0"
    //util
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'
    implementation 'com.google.android.gms:play-services-oss-licenses:17.0.0'
    implementation 'com.github.javiersantos:AppUpdater:2.7'
    implementation 'com.jakewharton.timber:timber:4.7.1'
    //testing
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.7'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

ktlint {
    disabledRules.set(["no-wildcard-imports"])
}